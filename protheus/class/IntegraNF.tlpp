#INCLUDE 'Rwmake.ch'
#INCLUDE 'Protheus.ch'
#INCLUDE 'Tbiconn.ch'
#INCLUDE 'Topconn.ch'
#Include 'totvs.ch'



/*/{Protheus.doc} IntegraNF
  (long_description)
  @author Leonardo Freitas
  @since 24/10/2025
  @version version
  /*/
Class IntegraNF
  
  public data cDocumentoNfe    as character
  public data cSerieNfe    as character
  public data aPvlNfs   as object
  public data aBloqueio as object

  public method new()
  public method integrar()

  private Method findOrCreateClient()
  private Method findOrCreateOrder()
  private Method unlockOrderItems()
  private Method invoiceOrder()
  private Method getTes()
  private Method validSerie()
  private Method calcImposto() 
  private Method getNatureza()
  private Method setEmpresa()
  private Method getClasPro()

EndClass

/*/{Protheus.doc} new
  (long_description)
  @author Leonardo Freitas
  @since 24/10/2025
  @version version
  @param param_name, param_type, param_descr
  @return return_var, return_type, return_description
  /*/
Method new() class IntegraNF
  
Return 

/*/{Protheus.doc} integrar
  (long_description)
  @author Leonardo Freitas
  @since 24/10/2025
  @version version
  @param param_name, param_type, param_descr
  @return return_var, return_type, return_description
  /*/
Method integrar(oPedido, nRecno_) class IntegraNF

  Local dBkpDtBase := ddatabase
  varError := ErrorClass():New()
  
  Default oPedido := JsonObject():new()

  TRY

    if oPedido:hasProperty("emissao")
      if !empty(oPedido["emissao"])
        aData := strtokarr(oPedido["emissao"], "/")
        dDataBase := sToD( aData[3] + aData[2] + aData[1] )
      endif
    endif

    if !oPedido:hasProperty("prestador")
      varError:genCode := 001
      varError:description := "Tag 'prestador' não localizado no json!"
      throw varError
    endif

    if !oPedido["prestador"]:hasProperty("cgc")
      varError:genCode := 001
      varError:description := "Tag 'cgc' não localizado no json!"
      throw varError
    endif

    if empty(oPedido["prestador"]["cgc"])
      varError:genCode := 001
      varError:description := "CNPJ da empresa não pode ser vazio!"
      throw varError 
    endif

    if !::setEmpresa(oPedido["prestador"]["cgc"])
      varError:genCode := 001
      varError:description := "Não foi possível setar a empresa para o CNPJ: " + alltrim(oPedido["prestador"]["cgc"]) + "!"
      throw varError
    endif

    if !oPedido:hasProperty("serieDocumento")
      varError:genCode := 001
      varError:description := "Tag 'serieDocumento' não localizado no json!"
      throw varError
    endif 

    self:cSerieNfe := oPedido["serieDocumento"]
    cYNumNF := oPedido["numeroDocumento"]

    self:validSerie()

    aCliente := self:findOrCreateClient(oObjeto := oPedido)

    if !aCliente[1]
      varError:genCode := 001
      varError:description := aCliente[2]
      throw varError
    endif

    recLock( "ZZ1", .F. )
      ZZ1->ZZ1_CLIENT := aCliente[2]
      ZZ1->ZZ1_LOJA := aCliente[3]
    ZZ1->( msUnlock() )

    aPedido_ := self:findOrCreateOrder(oPedido := oPedido, aClient := aCliente)

    if !aPedido_[1]
      varError:genCode := 001
      varError:description := aPedido_[2]
      throw varError
    endif

    cPedido := aPedido_[2]

    recLock( "ZZ1", .F. )
      ZZ1->ZZ1_CODPED := cPedido
    ZZ1->( msUnlock() )

    lItensPedido := self:unlockOrderItems(aPedido_[2])
    
    if !lItensPedido
      varError:genCode := 004
      varError:description := "Não foi possí­vel liberar os itens do pedido de venda!"
      throw varError
    endif

    recLock("ZZ1", .F.)
      ZZ1->ZZ1_LIBPED := .T.
    ZZ1->( msUnLock() )

    self:invoiceOrder(aPedido_[2])


    if empty(self:cDocumentoNfe)
      varError:genCode := 005
      varError:description := "Não foi possí­vel faturar o pedido de venda!"
      throw varError
    endif

    self:calcImposto(oObjeto := oPedido, aCliente := aCliente)

    recLock( "ZZ1", .F. )
      ZZ1->ZZ1_STATUS := "P"
      ZZ1->ZZ1_FILCTE := cFilAnt
      ZZ1->ZZ1_NUMCTE := self:cDocumentoNfe
      ZZ1->ZZ1_SERCTE := self:cSerieNfe
    ZZ1->( msUnlock() )

    ZZ2->( dbSetOrder(1) )
    recLock("ZZ2", .T.)
        ZZ2->ZZ2_FILIAL := ZZ1->ZZ1_FILIAL 
        ZZ2->ZZ2_codigo := getSx8Num("ZZ2", "ZZ2_CODIGO")
        ZZ2->ZZ2_codzz1 := ZZ1->ZZ1_CODIGO
        ZZ2->ZZ2_status := "P"
        ZZ2->ZZ2_dtpros := dDataBase
        ZZ2->ZZ2_hrpros := time()
        ZZ2->ZZ2_log    := "Integração processada com sucesso"
    ZZ2->( msUnLock() )

    

  CATCH oError

    recLock( "ZZ1", .F. )
      ZZ1->ZZ1_STATUS := "E"
    ZZ1->( msUnlock() )

    CHKFILE("ZZ2")

    DBSelectArea( "ZZ2" )
    recLock("ZZ2", .T.)
      ZZ2->ZZ2_FILIAL := ZZ1->ZZ1_FILIAL 
      ZZ2->ZZ2_codigo := getSx8Num("ZZ2", "ZZ2_CODIGO")
      ZZ2->ZZ2_codzz1 := ZZ1->ZZ1_CODIGO
      ZZ2->ZZ2_status := "E"
      ZZ2->ZZ2_dtpros := dDataBase
      ZZ2->ZZ2_hrpros := time()
      ZZ2->ZZ2_log    := oError:description
    ZZ2->( msUnLock() )

  EndTry

  dDataBase := dBkpDtBase

Return 

/*/{Protheus.doc} findOrCreateClient
  Retorna um cliente existente ou cria um cliente
  @author Leonardo Freitas
  @since 24/10/2025
  @version version
  @param param_name, param_type, param_descr
  @return return_var, return_type, return_description
  /*/
Method findOrCreateClient(oObjeto) class IntegraNF

  Local cCGC		:= ""
	Local cIE		:= ""
	Local cFantasia	:= ""
	Local cNome	:= ""
	Local cCep		:= ""
	Local cLogradouro	:= ""
	Local cMunicipio	:= ""
	Local cNumero	:= ""
	Local cEstado		:= ""
	Local cBairro	:= ""
	Local cCodMun_	:= ""
	Local cCodTel	:= ""
	Local cCodRegiao := ""
//  Local aSA1Auto := {}
//  Local aAI0Auto := {}
  Local aRetorno_ := {}
//  local nOpcAuto := 3

  Private lMsErroAuto := .F.

  if oObjeto["cliente"]:hasProperty("cgc")
    cCGC := oObjeto["cliente"]["cgc"]
  else 
    return { .F., "É necessário enviar o cpf/cnpj do cliente!" }
  endif

  if oObjeto["cliente"]:hasProperty("cgc")
    cCGC := oObjeto["cliente"]["cgc"]
  else 
    return { .F., "É necessário enviar o cpf/cnpj do cliente!" }
  endif
  
  if oObjeto["cliente"]:hasProperty("ie")
    cIE := oObjeto["cliente"]["ie"]
  endif

  if oObjeto["cliente"]:hasProperty("nome")
    cNome := oObjeto["cliente"]["nome"]
  else 
    return { .F., "É necessário enviar o nome do cliente!" }
  endif

  if oObjeto["cliente"]:hasProperty("fantasia")
    cFantasia := UPPER( substr( oObjeto["cliente"]["fantasia"] , 1, TAMSX3("A1_NREDUZ")[1] ) )
  endif
  
  if empty(cFantasia)
    cFantasia := UPPER( substr( cNome , 1, TAMSX3("A1_NREDUZ")[1] ) )
  endif

  if oObjeto["cliente"]:hasProperty("telefone")
    cCodTel := oObjeto["cliente"]["telefone"]
  endif

  if oObjeto["cliente"]:hasProperty("endereco")
    
    if oObjeto["cliente"]["endereco"]:hasProperty("cep")
      cCep := oObjeto["cliente"]["endereco"]["cep"]
    else 
      return { .F., "É necessário enviar o cep do endereço!" }
    endif
  
    if oObjeto["cliente"]["endereco"]:hasProperty("logradouro")
      cLogradouro := oObjeto["cliente"]["endereco"]["logradouro"]
    else 
      return { .F., "É necessário enviar o logradouro do endereço!" }
    endif

    if oObjeto["cliente"]["endereco"]:hasProperty("municipio")
      cMunicipio := oObjeto["cliente"]["endereco"]["municipio"]
    else 
      return { .F., "É necessário enviar o municipio do endereço!" }
    endif

    if oObjeto["cliente"]["endereco"]:hasProperty("bairro")
      cBairro := oObjeto["cliente"]["endereco"]["bairro"]
    else 
      return { .F., "É necessário enviar o bairro do endereço!" }
    endif

    if oObjeto["cliente"]["endereco"]:hasProperty("estado")
      cEstado := oObjeto["cliente"]["endereco"]["estado"]
    else 
      return { .F., "É necessário enviar o estado do endereço!" }
    endif

    if oObjeto["cliente"]["endereco"]:hasProperty("numero")
      cNumero := oObjeto["cliente"]["endereco"]["numero"]
    else 
      return { .F., "É necessário enviar o numero do endereço!" }
    endif
  
    cCodMun_ := POSICIONE( "CC2", 4, xFilial("CC2") + cEstado + UPPER(alltrim( cMunicipio )), "CC2_CODMUN" )

    //Caso não localize o código do municipio irá retornar uma falha e não seguirá no processo
    if empty(cCodMun_)
      return { .F.,  'Não foi localizado o código do municipio: ' + CRLF + "Municipio: " + alltrim(cMun) + CRLF + "Estado: " + cEst}
    endif
  
  else 
    return { .F., "É necessário enviar o endereço!" }
  endif

  
  SA1->( dbSetOrder(3) )
  if SA1->( dbSeek( xFilial( "SA1" ) + cCGC ) )

    recLock( "SA1", .F. )
      
    if oObjeto:hasProperty('valorIssRetido')
      if oObjeto["valorIssRetido"] > 0
        SA1->A1_RECISS := "1"
      else
        SA1->A1_RECISS := "2"
      endif
    endif 
    SA1->A1_INCISS := "S"

    if oObjeto:hasProperty('valorIRRF')
      if oObjeto["valorIRRF"] > 0
        SA1->A1_RECIRRF := "1"
      else
        SA1->A1_RECIRRF := "2"
      endif
    endif 

    if oObjeto:hasProperty('valorPIS')
      SA1->A1_RECPIS := IIF(oObjeto["valorPIS"] > 0, "S", "N" )
    endif 

    if oObjeto:hasProperty('valorCOFINS')
      if oObjeto["valorCOFINS"] > 0
        SA1->A1_RECCOFI := "S"
      else 
        SA1->A1_RECCOFI := "N"
      endif
    endif 

    if oObjeto:hasProperty('valorCSLL')
      SA1->A1_RECCSLL := IIF(oObjeto["valorCSLL"] > 0, "S", "N" )
    endif 

    if oObjeto:hasProperty('valorINSS')
      SA1->A1_RECINSS := IIF(oObjeto["valorINSS"] > 0, "S", "N" )
    endif 
    
    SA1->( msUnLock() )

    return { .T., SA1->A1_COD, SA1->A1_LOJA }

  endif

  cCod := getSx8Num("SA1", "A1_COD")
  
  lDeuCerto := .F.
    
  //Pegando o modelo de dados, setando a operação de inclusão
  oModel := FWLoadModel("CRMA980") //"MATA030" na antiga MATA030
  oModel:SetOperation(3)
  oModel:Activate()
    
  //Pegando o model dos campos da SA1
  oSA1Mod:= oModel:getModel("SA1MASTER") //"MATA030_SA1" na antiga MATA030
  oSA1Mod:setValue("A1_COD",       cCod        ) // Codigo 
  oSA1Mod:setValue("A1_LOJA",      '01'       ) // Loja
  oSA1Mod:setValue("A1_NOME",      Substr(cNome,1,TAMSX3("A1_NOME")[1])       ) // Nome             
  oSA1Mod:setValue("A1_NREDUZ",    Substr(cFantasia,1,TAMSX3("A1_NREDUZ")[1])   ) // Nome reduz. 
  oSA1Mod:setValue("A1_END",       Substr(cLogradouro,1,TAMSX3("A1_END")[1])   ) // Endereco
  oSA1Mod:setValue("A1_BAIRRO",    Substr(cBairro,1,TAMSX3("A1_BAIRRO")[1])     ) // Bairro
  oSA1Mod:setValue("A1_TIPO",      "F"         ) // Tipo 
  oSA1Mod:setValue("A1_EST",       cEstado        ) // Estado
  oSA1Mod:setValue("A1_COD_MUN",   Substr(cCodMun_,1,TAMSX3("A1_COD_MUN")[1])     ) // Codigo Municipio                
  oSA1Mod:setValue("A1_MUN",       Substr(cMunicipio,1,TAMSX3("A1_MUN")[1])    ) // Municipio
  oSA1Mod:setValue("A1_CEP",       Substr(cCEP,1,TAMSX3("A1_CEP")[1])        ) // CEP
  oSA1Mod:setValue("A1_INSCR",     cIE         ) // Inscricao Estadual
  oSA1Mod:setValue("A1_CGC",       cCGC       ) // CNPJ/CPF            
  //oSA1Mod:setValue("A1_PAIS",      cCodPais    ) // Pais            
  //oSA1Mod:setValue("A1_EMAIL",     cEMail      ) // E-Mail
  oSA1Mod:setValue("A1_DDD",        Substr(cCodTel,1,2)        ) // DDD            
  oSA1Mod:setValue("A1_TEL",       Substr(cCodTel,3,TAMSX3("A1_TEL")[1])   ) // Fone                 
  oSA1Mod:setValue("A1_PESSOA",    IIF(LEN(cCGC) == 14, "J", "F")  ) // Tipo Pessoa
  oSA1Mod:setValue("A1_CODPAIS", "01058" )
  oSA1Mod:setValue("A1_CDRDES", cCodRegiao )

  //oSA1Mod:setValue("A1_INCISS", "1" )

  if oObjeto:hasProperty('valorIssRetido')
    if oObjeto["valorIssRetido"] > 0
      oSA1Mod:setValue("A1_RECISS", "1" )
    else
      oSA1Mod:setValue("A1_RECISS", "2" )
    endif
  endif 

  oSA1Mod:setValue("A1_INCISS", "S" )

  if oObjeto:hasProperty('valorIRRF')
    if oObjeto["valorIRRF"] > 0
      oSA1Mod:setValue("A1_RECIRRF", "1" )
    else
      oSA1Mod:setValue("A1_RECIRRF", "2" )
    endif
  endif 

  if oObjeto:hasProperty('valorPIS')
    if oObjeto["valorPIS"] > 0
      oSA1Mod:setValue("A1_RECPIS", "S" )
    else
      oSA1Mod:setValue("A1_RECPIS", "N" )
    endif
  endif 

  if oObjeto:hasProperty('valorCOFINS')
    if oObjeto["valorCOFINS"] > 0
      oSA1Mod:setValue("A1_RECCOFI", "S" )
    else
      oSA1Mod:setValue("A1_RECCOFI", "N" )
    endif
  endif 

  if oObjeto:hasProperty('valorCSLL')
    if oObjeto["valorCSLL"] > 0
      oSA1Mod:setValue("A1_RECCSLL", "S" )
    else
      oSA1Mod:setValue("A1_RECCSLL", "N" )
    endif
  endif 

  if oObjeto:hasProperty('valorINSS')
    if oObjeto["valorINSS"] > 0
      oSA1Mod:setValue("A1_RECINSS", "S" )
    else
      oSA1Mod:setValue("A1_RECINSS", "N" )
    endif
  endif 

  //Se conseguir validar as informações
  If oModel:VldData()
      
    //Tenta realizar o Commit
    If oModel:CommitData()
        lDeuCerto := .T.
          
    //Se não deu certo, altera a variável para false
    Else
        lDeuCerto := .F.
    EndIf
      
  //Se não conseguir validar as informações, altera a variável para false
  Else
      lDeuCerto := .F.
  EndIf
    
  //Se não deu certo a inclusão, mostra a mensagem de erro
  If ! lDeuCerto
      //Busca o Erro do Modelo de Dados
      aErro := oModel:GetErrorMessage()
        
      //Monta o Texto que será mostrado na tela
      AutoGrLog("Id do formulário de origem:"  + ' [' + AllToChar(aErro[01]) + ']')
      AutoGrLog("Id do campo de origem: "      + ' [' + AllToChar(aErro[02]) + ']')
      AutoGrLog("Id do formulário de erro: "   + ' [' + AllToChar(aErro[03]) + ']')
      AutoGrLog("Id do campo de erro: "        + ' [' + AllToChar(aErro[04]) + ']')
      AutoGrLog("Id do erro: "                 + ' [' + AllToChar(aErro[05]) + ']')
      AutoGrLog("Mensagem do erro: "           + ' [' + AllToChar(aErro[06]) + ']')
      AutoGrLog("Mensagem da solução: "        + ' [' + AllToChar(aErro[07]) + ']')
      AutoGrLog("Valor atribuído: "            + ' [' + AllToChar(aErro[08]) + ']')
      AutoGrLog("Valor anterior: "             + ' [' + AllToChar(aErro[09]) + ']')
        
      //Mostra a mensagem de Erro
      RollBAckSx8()

      cArquivo := FwTimeStamp() + "_erro_cliente_execauto.txt"
      MostraErro( GetSrvProfString("Startpath","") , cArquivo )
      cMsg := MemoRead(  GetSrvProfString("Startpath","") + '\' + cArquivo )

      aRetorno_ := { .F., cMsg }
  else 

    ConfirmSX8()
    aRetorno_ := { .T., SA1->A1_COD, SA1->A1_LOJA }  

  EndIf

  //Desativa o modelo de dados
  oModel:DeActivate()


Return aRetorno_

/*/{Protheus.doc} findOrCreateOrder
  Retorna um pedido de venda existente ou cria um pedido de venda
  @author Leonardo Freitas
  @since 27/10/2025
  @version version
  @param param_name, param_type, param_descr
  @return return_var, return_type, return_description
  /*/
Method findOrCreateOrder(oPedido, aClient) class IntegraNF
  
  Local _cNatureza := Alltrim(SuperGetMv("MS_NATNFS",.F.,"010102"))
  Local cTes := '512'
  local dEmissao := ddatabase
  local cUFDest_ := ""
  local cCodMunPrest := ""
//  Local cCfop := ""
//  Local nPesoBruto := 0

  Private lMsErroAuto := .F.
  Private cCond     := SuperGetMv("MS_CONDTRA",.F.,"000")

  if !empty(ZZ1->ZZ1_CODPED)
    return { .T., ZZ1->ZZ1_CODPED }
  endif

  cDoc := GetSxeNum("SC5", "C5_NUM")
  ConfirmSX8()
  //RollBAckSx8()
  aCabec   := {}
  aItens   := {}
  aLinha   := {}

  if !oPedido:hasProperty("servico")
    return { .F., "Tag 'servico' não localizado no json!" }
  endif

  if !oPedido["servico"]:hasProperty("codigo")
    return { .F., "Tag 'codigo' do servico não localizado no json!" }
  endif

  if oPedido:hasProperty("emissao")
    if !empty(oPedido["emissao"])
      aData := strtokarr(oPedido["emissao"], "/")
      dEmissao := sToD( aData[3] + aData[2] + aData[1] )
    endif
  endif

  if oPedido:hasProperty("ufPrest")
    cUFDest_ := alltrim(oPedido["ufPrest"])
  endif

  if oPedido:hasProperty("CodMunPrest")
    cCodMunPrest := alltrim(oPedido["CodMunPrest"])
  endif
/*
  if oPedido:hasProperty("codigoCfop")
    cCfop := alltrim(oPedido["codigoCfop"])
  endif

  if empty(cCfop)
    return { .F., "Tag 'codigoCfop' do servico não localizado no json!" }
  endif

  if oPedido:hasProperty("codigoClasFis")
    cClasFis := alltrim(oPedido["codigoClasFis"])
  endif

  if empty(cClasFis)
    return { .F., "Tag 'cClasFis' do servico não localizado no json!" }
  endif
  
  //cTes := '512'//self:getTes(cCfop, cClasFis)

  if empty(cTes)
    return { .F., 'TES nao localizada para o CFOP ' + cCFOP }
  endif

*/
  _cNatureza := ::getNatureza(oPedido := oPedido)

  SB1->( dbSetOrder( 1 ) )
  if SB1->( dbSeek( xFilial( "SB1" ) + alltrim( oPedido["servico"]["codigo"] ) ) )
    
    aadd(aLinha,{"C6_ITEM"   , StrZero(1,2), Nil})
    aadd(aLinha,{"C6_PRODUTO", SB1->B1_COD       , Nil})
//    aadd(aLinha,{"C6_LOCAL", "01"       , Nil})
//    aadd(aLinha,{"C6_UNID", "UN"       , Nil})
//    aadd(aLinha,{"C6_CLASFIS" , "040"            , Nil})
//    aadd(aLinha,{"C6_TPOP" , "F"            , Nil})
    aadd(aLinha,{"C6_QTDVEN" , 1            , Nil})
    aadd(aLinha,{"C6_QTDLIB" , 1            , Nil})
    aadd(aLinha,{"C6_PRCVEN" , oPedido["valorServico"]         , Nil})
    aadd(aLinha,{"C6_PRUNIT" , oPedido["valorServico"]         , Nil})
//    aadd(aLinha,{"C6_VALOR"  , oPedido["valorServico"]         , Nil})
    aadd(aLinha,{"C6_TES"    , cTes       , Nil})

//    aadd(aLinha,{"C6_CF" , cCFOP            , Nil})

//    nPesoBruto += SB1->B1_PESBRU

    aadd(aItens, aLinha)

  else 

    return { .F., "Nao foi possível localizar o produto no Protheus!" }

  endif

  SA1->( dbSetOrder( 1 ) )
  SA1->( dbSeek( xFilial( "SA1" ) + aClient[2] + aClient[3] ) )

  aadd(aCabec, {"C5_NUM"    , cDoc     , Nil})
  aadd(aCabec, {"C5_TIPO"   , "N"      , Nil})
  aadd(aCabec, {"C5_CLIENTE", aClient[2]  , Nil})
  aadd(aCabec, {"C5_LOJACLI", aClient[3] , Nil})
  aadd(aCabec, {"C5_LOJAENT", aClient[3] , Nil})
  aadd(aCabec, {"C5_EMISSAO", dEmissao, nil })
  aadd(aCabec, {"C5_CONDPAG", cCond, Nil})                
  aadd(aCabec, {"C5_TIPOCLI", SA1->A1_TIPO, Nil})                
//  aadd(aCabec, {"C5_UFDEST", cUFDest_, Nil})                
  aadd(aCabec, {"C5_CLASFIS", POSICIONE("SF4", 1, xFilial("SF4") + cTes, "F4_CSTISS"), Nil}) 

  if !empty(cCodMunPrest)
    aadd(aCabec, {"C5_ESTPRES", cUFDest_, Nil})                
    aadd(aCabec, {"C5_MUNPRES", substr(alltrim(cCodMunPrest), 3, len(alltrim(cCodMunPrest)) - 2), Nil})                

  endif


//  aadd(aCabec, {"C5_TPCARGA", "2", Nil})                
//  aadd(aCabec, {"C5_NOMECLI", UPPER( subStr( fwNoaccent( SA1->A1_NOME ), 1, TAMSX3("C5_NOMECLI")[1] ) ), Nil})
//  aadd(aCabec, {"C5_CODWEB", iif(!empty( cPackId ), cPackId, idMercadoLivre), Nil})                
//  aadd(aCabec, {"C5_MENNOTA", "COMPRA #" + iif(!empty( cPackId ), cPackId, idMercadoLivre) + " MERCADO LIVRE", Nil})                
  
  
//  aadd(aCabec, {"C5_VEND1", "000087", Nil})                
//  aadd(aCabec, {"C5_COMIS1", 1.00, Nil})                
  aadd(aCabec, {"C5_NATUREZ", _cNatureza, Nil})                
//  aadd(aCabec, {"C5_VOLUME1", 1, Nil})                
//  aadd(aCabec, {"C5_ESPECI1", "NFS", Nil})                
//  aadd(aCabec, {"C5_TIPLIB", "2", Nil})                
//  aadd(aCabec, {"C5_MOEDA", 1, Nil})                
//  aadd(aCabec, {"C5_DESPESA", 0, Nil})                
//  aadd(aCabec, {"C5_ACRSFIN", /* nAcrescimo */ 0, Nil}) //acrescimo financeiro     

//  aadd(aCabec, {"C5_PBRUTO", nPesoBruto, Nil})                
//  aadd(aCabec, {"C5_PESOL", nPesoBruto, Nil})  

//  aShipping := self:getRequest(" /shipments/" + cValToChar(oOrder["shipping"]["id"]) + "/costs")

//  if !aShipping[1] 
//    return {.F., "Não foi possível buscar o valor do frete"}
//  endif

//  aadd(aCabec, {"C5_VLR_FRT", aShipping[2]["receiver"]["cost"], Nil})                
//  aadd(aCabec, {"C5_FRETE", 0.00 /*aShipping[2]["receiver"]["cost"]*/, Nil})                
//  aadd(aCabec, {"C5_TRANSP", iif(aShipping[2]["logistic_type"] == "drop_off", "000962", "001531"), Nil})                
//  aadd(aCabec, {"C5_TPFRETE", "C", Nil})                

  CONOUT("Iniciando a gravacao")
  MSExecAuto({|a, b, c, d| MATA410(a, b, c, d)}, aCabec, aItens, 3, .F.)

  If !lMsErroAuto
      //nPed := nPed + 1
    ConOut("Incluido com sucesso! Pedido: " + cDoc)
    
    return { .T., cDoc }
  Else
    cArquivo := FwTimeStamp() + "_mercado_livre_execauto.txt"
    MostraErro( GetSrvProfString("Startpath","") , cArquivo )
    cMsg := MemoRead(  GetSrvProfString("Startpath","") + '\' + cArquivo )

      
    return { .F., cMsg }
  EndIf

Return { .F., "" }

/*/{Protheus.doc} unlockOrderItems
  (long_description)
  @author user
  @since 31/07/2025
  @version version
  @param param_name, param_type, param_descr
  @return return_var, return_type, return_description
  /*/
Method unlockOrderItems(cNumeroPedido) class IntegraNF

  Local aPedItens := {}

  SC9->(dbSetOrder(1))
  SC6->(dbsetorder(1))

  //Realizo liberacao de credito/estoque/wms
  If SC6->(DbSeek(cSeek_ := xFilial("SC6")+ cNumeroPedido))
    While SC6->(!EOF()) .And. SC6->C6_FILIAL + SC6->C6_NUM == cSeek_

      If SC9->(dbseek(xFilial("SC9")+SC6->C6_NUM+SC6->C6_ITEM))
        recLock("SC9",.F.)
          SC9->C9_BLCRED	:= "  "
          SC9->C9_BLEST	:= "  "
          SC9->C9_BLWMS	:= "  "
        SC9->(msUnLock())
      endif

      SC5->(dbSeek(xFilial("SC5") + SC9->C9_PEDIDO))
			SC6->(dbSeek(xFilial("SC6") + SC9->C9_PEDIDO + SC9->C9_ITEM + SC9->C9_PRODUTO))
			SE4->(dbSeek(xFilial("SE4") + SC5->C5_CONDPAG))
			SB1->(dbSeek(xFilial("SB1") + SC6->C6_PRODUTO))
			SB2->(dbSeek(xFilial("SB2") + SC9->C9_PRODUTO + SC9->C9_LOCAL))
			SF4->(dbSeek(xFilial("SF4") + SC6->C6_TES))
			DA3->(dbSeek(xFilial('DA3') + SC5->C5_VEICULO))
			SA4->(dbSeek(xFilial('DA3') + SC5->C5_TRANSP))
			SA1->(dbSeek(xFilial('SA1') + SC5->C5_CLIENTE+SC5->C5_LOJACLI))


      aAdd(aPedItens, {;
        SC9->C9_PEDIDO,; 
        SC9->C9_ITEM,;
        SC9->C9_SEQUEN,;
        SC9->C9_QTDLIB,;
        SC9->C9_PRCVEN,;
        SC9->C9_PRODUTO,;
        SF4->F4_ISS == "S",;
        SC9->(recno()),;
        SC5->(recno()),;
        SC6->(recno()),;
        SE4->(recno()),;
        SB1->(recno()),;
        SB2->(recno()),;
        SF4->(recno())} )

      SC6->(dbskip())
    EndDo
  Endif


  self:aPvlNfs := aPedItens
  
  if ZZ1->ZZ1_LIBPED
    return .T.
  endif


  // Liberacao de pedido (Rotina Padrão do sistema)
  Ma410LbNfs(2,@self:aPvlNfs,@self:aBloqueio)

  //Realizo liberacao de credito/estoque/wms
  If SC6->(DbSeek(cSeek_ := xFilial("SC6")+ cNumeroPedido))
        While SC6->(!EOF()) .And. SC6->C6_FILIAL + SC6->C6_NUM == cSeek_

          If SC9->(dbseek(xFilial("SC9")+SC6->C6_NUM+SC6->C6_ITEM))
              recLock("SC9",.F.)
                  SC9->C9_BLCRED	:= "  "
                  SC9->C9_BLEST	:= "  "
                  SC9->C9_BLWMS	:= "  "
              SC9->(msUnLock())
          endif

          SC6->(dbskip())
      EndDo
  Endif
  
  //-- Checa itens liberados
  Ma410LbNfs( 1, @self:aPvlNfs, @self:aBloqueio )

Return ( Empty(self:aBloqueio) .and. !Empty(self:aPvlNfs) )

/*/{Protheus.doc} invoiceOrder
  (long_description)
  @author Leonardo Freitas
  @since 11/12/2025
  @version version
  @param param_name, param_type, param_descr
  @return return_var, return_type, return_description
  /*/
Method invoiceOrder(cNumeroPedido) class IntegraNF

  self:cDocumentoNfe := ""

  SD2->( dbSetOrder( 8 ) )
  if SD2->( dbSeek( xFilial( "SD2" ) + cNumeroPedido ) )
    self:cDocumentoNfe := SD2->D2_DOC
  endif

  //É necessário carregar o grupo de perguntas MT460A, se não será executado com os valores default.
  Pergunte("MT460A",.F.)

  SetFunName("MATA461")

  self:cDocumentoNfe := MaPvlNfs(self:aPvlNfs,;
				   self:cSerieNfe,;
				   .F. /*lMostraCtb*/,;
				   .F. /*lAglutCtb*/,;
				   .F. /*lCtbOnLine*/,;
				   .F. /*lCtbCusto*/,;
				   .F. /*lReajuste*/,;
					0  /*nCalAcrs*/,;
					0  /*nArredPrcLis*/,;
				   .F. /*lAtuSA7*/,;
				   .F. /*lECF*/)
Return self:cDocumentoNfe


/*/{Protheus.doc} getTes
  (long_description)
  @author user
  @since 15/12/2025
  @version version
  @param param_name, param_type, param_descr
  @return return_var, return_type, return_description
  /*/
Method getTes(cCFOP, cClasFis_) class IntegraNF
  local cRet := ""
  cCFOP := SUBSTR(cCFOP,2,3)

  cQuery := " SELECT F4_CODIGO FROM " + RetSqlTab('SF4')
  cQuery += " WHERE D_E_L_E_T_ = ' '"
  cQuery += " AND SUBSTR(F4_CF,2,3) = '" + cCFOP + "' "
  cQuery += " AND F4_TIPO    = 'S'"  //TES DE SAIDA
  cQuery += " AND F4_DUPLIC  = 'S' " //TEM QUE GERAR DUPLICATA
  cQuery += " AND F4_ESTOQUE = 'N'"  //NAO PODE MOVIMENTAR ESTOQUE
  if alltrim(cClasFis_) $ '40,41,60'
      cQuery += " AND F4_SITTRIB = '" + alltrim(cClasFis_) + "' "
  endif

  If Select('QRYTES') > 0
      QRYTES->(DbCloseArea())
  EndIf

  TcQuery cQuery New Alias 'QRYTES'

  If QRYTES->(!Eof())
      cRet := QRYTES->F4_CODIGO
  EndIf
  QRYTES->( dbCloseArea() )

Return cRet

/*/{Protheus.doc} methodName
  (long_description)
  @author user
  @since 16/12/2025
  @version version
  @param param_name, param_type, param_descr
  @return return_var, return_type, return_description
  /*/
Method validSerie() class IntegraNF
    
    SD9->( dbSetOrder( 1 ) )
    if !SD9->( dbSeek( cFilAnt + alltrim(self:cSerieNfe) ) )
        recLock("SD9", .T.)
            SD9->D9_FILIAL := cFilAnt
            SD9->D9_SERIE := alltrim(self:cSerieNfe)
            SD9->D9_DOC := PADL("1",TAMSX3("F2_DOC")[1],"0")
        SD9->( msUnlock() )
    endif

    SX5->( dbSetOrder( 1 ) )
    if !SX5->( dbSeek( xFilial( "SX5" ) + "SN" + PADR(alltrim(self:cSerieNfe),TAMSX3("F2_SERIE")[1]," ") ) )
        recLock("SX5", .T.)
            SX5->X5_FILIAL := xFilial( "SX5" )
            SX5->X5_TABELA := "SN"
            SX5->X5_CHAVE := alltrim(self:cSerieNfe)
            SX5->X5_DESCRI := alltrim(self:cSerieNfe) + '=NFS'
            SX5->X5_DESCSPA := alltrim(self:cSerieNfe) + '=NFS'
            SX5->X5_DESCENG := alltrim(self:cSerieNfe) + '=NFS'
        SX5->( msUnLock() )
    endif

Return 

/*/{Protheus.doc} calcImposto
  (long_description)
  @author user
  @since 18/12/2025
  @version version
  @param param_name, param_type, param_descr
  @return return_var, return_type, return_description
  /*/
Method calcImposto(oObjeto, aCliente) class IntegraNF

  local dEmissao := ddatabase 
  local cClasProd := ""
  Local cAtivCPB   := SuperGetMv('MS_ATIVCPB',.F., '00000140')
  Local nAliqCPB := SuperGetMv('MS_ALIQCPB', .F., 1.5)

  if oObjeto:hasProperty("emissao")
    if !empty(oObjeto["emissao"])
      aData := strtokarr(oObjeto["emissao"], "/")
      dEmissao := sToD( aData[3] + aData[2] + aData[1] )
    endif
  endif

  if oObjeto:hasProperty("productId")
    cClasProd := ::getClasPro(oObjeto["productId"])
  endif

  //Altera informações de impostos na SF2 
  SF2->( dbSetOrder( 1 ) )
  if SF2->(dbSeek(xFilial("SF2") + PADR(alltrim(self:cDocumentoNfe),TAMSX3("F2_DOC")[1]," ") + PADR(alltrim(self:cSerieNfe),TAMSX3("F2_SERIE")[1]," ") + PADR(alltrim(aCliente[2]),TAMSX3("A1_COD")[1]," ") + aCliente[3]))
    recLock("SF2", .F.)
      SF2->F2_ESPECIE := "NFS"
      SF2->F2_CLASPRO := cClasProd

      SF2->F2_EMISSAO := dEmissao

      SF2->F2_BASEISS := oObjeto["baseISS"]
      SF2->F2_VALISS := oObjeto["valorISS"]

      if oObjeto:hasProperty('valorIssRetido')
        if oObjeto["valorIssRetido"] > 0
          SF2->F2_RECISS := "1"
        else
          SF2->F2_RECISS := "2"
        endif
      endif
      
      SF2->F2_BASEINS := oObjeto["baseINSS"]
      SF2->F2_VALINSS := oObjeto["valorINSS"]

      SF2->F2_BASCSLL := oObjeto["baseCSLL"]
      SF2->F2_VALCSLL := oObjeto["valorCSLL"]

      SF2->F2_BASCOFI := oObjeto["baseCOFINS"]
      SF2->F2_VALCOFI := oObjeto["valorCOFINS"] 

      SF2->F2_BASPIS := oObjeto["basePIS"]
      SF2->F2_VALPIS := oObjeto["valorPIS"]

      SF2->F2_BASEIRR := oObjeto["baseIRRF"]
      SF2->F2_VALIRRF := oObjeto["valorIRRF"]

      
    SF2->( msUnlock() )
  endif

  SD2->( dbSetOrder( 3 ) )
  if SD2->(dbSeek(xFilial("SD2") + PADR(alltrim(self:cDocumentoNfe),TAMSX3("D2_DOC")[1]," ") + PADR(alltrim(self:cSerieNfe),TAMSX3("D2_SERIE")[1]," ") + PADR(alltrim(aCliente[2]),TAMSX3("A1_COD")[1]," ") + aCliente[3]))
    recLock("SD2", .F.)

      SD2->D2_EMISSAO := dEmissao

      SD2->D2_BASEISS := oObjeto["baseISS"]
      SD2->D2_VALISS := oObjeto["valorISS"]
      SD2->D2_ALIQISS := ( oObjeto["valorISS"] * 100 ) / oObjeto["baseISS"]
      
      SD2->D2_BASEINS := oObjeto["baseINSS"]
      SD2->D2_VALINS := oObjeto["valorINSS"]
      SD2->D2_ALIQINS := ( oObjeto["valorINSS"] * 100 ) / oObjeto["baseINSS"]

      SD2->D2_BASECSL := oObjeto["baseCSLL"]
      SD2->D2_ALQCSL := ( oObjeto["valorCSLL"] * 100 ) / oObjeto["baseCSLL"]
      SD2->D2_VALCSL := oObjeto["valorCSLL"]

      SD2->D2_BASECOF := oObjeto["baseCOFINS"]
      SD2->D2_ALQCOF := ( oObjeto["valorCOFINS"] * 100 ) / oObjeto["baseCOFINS"]
      SD2->D2_VALCOF := oObjeto["valorCOFINS"] 

      SD2->D2_BASEPIS := oObjeto["basePIS"]
      SD2->D2_ALQPIS := ( oObjeto["valorPIS"] * 100 ) / oObjeto["basePIS"]
      SD2->D2_VALPIS := oObjeto["valorPIS"]

      SD2->D2_BASEIRR := oObjeto["baseIRRF"]
      SD2->D2_ALQIRRF := ( oObjeto["valorIRRF"] * 100 ) / oObjeto["baseIRRF"]
      SD2->D2_VALIRRF := oObjeto["valorIRRF"]
    SD2->( msUnlock() )
  endif

  SE1->( dbSetOrder( 1 ) )

  //Encontra o titulo da nota fiscal de serviço para fazer os ajustes nos campos de impostos
  if SE1->(dbSeek( cSeek_ :=  xFilial("SE1") + PADR(alltrim(self:cSerieNfe),TAMSX3("F2_SERIE")[1]," ") + PADR(alltrim(self:cDocumentoNfe),TAMSX3("F2_DOC")[1]," ") + space( TAMSX3("E1_PARCELA")[1] ) + "NF"))
    if !empty(SE1->E1_SDOC)
      recLock("SE1", .F.)
        SE1->E1_BASEIRF := oObjeto["baseIRRF"]
        SE1->E1_IRRF := oObjeto["valorIRRF"]

        SE1->E1_BASEPIS := oObjeto["basePIS"]
        SE1->E1_PIS := oObjeto["valorPIS"]

        SE1->E1_BASECOF := oObjeto["baseCOFINS"]
        SE1->E1_COFINS := oObjeto["valorCOFINS"]
        
        SE1->E1_BASECSL := oObjeto["baseCSLL"]
        SE1->E1_CSLL := oObjeto["valorCSLL"]
        
        SE1->E1_BASEINS := oObjeto["baseINSS"]
        SE1->E1_INSS := oObjeto["valorINSS"]

        //SE1->E1_BASEINS := oObjeto["baseISS"]
        SE1->E1_ISS := oObjeto["valorIssRetido"]
        SE1->E1_VRETISS := oObjeto["valorIssRetido"]
      SE1->( msUnlock() )
    endif
  endif

  SF3->( dbSetOrder( 4 ) )
  if SF3->(dbSeek(cSeek_ := xFilial("SF3") + aCliente[2] + aCliente[3] + PADR(alltrim(self:cDocumentoNfe),TAMSX3("F2_DOC")[1]," ") + PADR(alltrim(self:cSerieNfe),TAMSX3("F2_SERIE")[1]," ") ))
    while SF3->(!EOF()) .and. SF3->F3_FILIAL + SF3->F3_CLIEFOR + SF3->F3_LOJA + SF3->F3_NFISCAL + SF3->F3_SERIE == cSeek_
      recLock("SF3", .F.)
        SF3->F3_ATIVCPB := cAtivCPB
        SF3->F3_ALIQCPB := nAliqCPB
        SF3->F3_VALCPB := Round(SF2->F2_VALMERC * (nAliqCPB / 100),2)
        SF3->F3_BASECPB := SF2->F2_VALMERC
        SF3->F3_ESPECIE := "NFS"
        if !empty(SF3->F3_CODISS)
          if oObjeto:hasProperty('valorIssRetido')
            if oObjeto["valorIssRetido"] > 0
              SF3->F3_ICMSRET := oObjeto["valorIssRetido"]
              SF3->F3_VALICM := oObjeto["valorIssRetido"]
            else
              SF3->F3_VALICM := oObjeto["valorISS"]
            endif
          else
            SF3->F3_VALICM := oObjeto["valorISS"]
          endif
          SF3->F3_BASEICM := oObjeto["baseISS"]
          SF3->F3_ALIQICM := ( oObjeto["valorISS"] * 100 ) / oObjeto["baseISS"]
        endif
      SF3->( msUnlock() )
      SF3->(dbSkip())
    enddo
  endif

  SFT->( dbSetOrder( 15 ) )
  if SFT->(dbSeek(xFilial("SFT") + PADR(alltrim(self:cDocumentoNfe),TAMSX3("F2_DOC")[1]," ") + PADR(alltrim(self:cSerieNfe),TAMSX3("F2_SERIE")[1]," ") + aCliente[2] + aCliente[3]))
    recLock("SFT", .F.)
      SFT->FT_ESPECIE := "NFS"

      SFT->FT_ATIVCPB := cAtivCPB
      SFT->FT_ALIQCPB := nAliqCPB
      SFT->FT_VALCPB  := Round(SF2->F2_VALMERC * nAliqCPB / 100,2)
      SFT->FT_BASECPB := SF2->F2_VALMERC   

      SFT->FT_ALIQICM := ( oObjeto["valorISS"] * 100 ) / oObjeto["baseISS"]
      SFT->FT_BASEICM := oObjeto["baseISS"]
      SFT->FT_VALICM := oObjeto["valorISS"]

      if oObjeto:hasProperty('valorIssRetido')
        if oObjeto["valorIssRetido"] > 0
          SFT->FT_RECISS := "1"
          SFT->FT_ICMSRET := oObjeto["valorIssRetido"]
          SFT->FT_VALICM := oObjeto["valorIssRetido"]
        else
          SFT->FT_RECISS := "2"
        endif
      else
        SFT->FT_VALICM := oObjeto["valorISS"]
      endif

      SFT->FT_ALIQIRR := ( oObjeto["valorIRRF"] * 100 ) / oObjeto["baseIRRF"]
      SFT->FT_BASEIRR := oObjeto["baseIRRF"]
      SFT->FT_VALIRR  := oObjeto["valorIRRF"]

      SFT->FT_ALIQINS := ( oObjeto["valorINSS"] * 100 ) / oObjeto["baseINSS"]
      SFT->FT_BASEINS := oObjeto["baseINSS"]
      SFT->FT_VALINS  := oObjeto["valorINSS"]

      SFT->FT_ALIQPIS := ( oObjeto["valorPIS"] * 100 ) / oObjeto["basePIS"]
      SFT->FT_BASEPIS := oObjeto["basePIS"]
      SFT->FT_VALPIS  := oObjeto["valorPIS"]
      SFT->FT_BRETPIS := oObjeto["basePIS"]
      SFT->FT_VRETPIS := oObjeto["valorPIS"]

      SFT->FT_ALIQCOF := ( oObjeto["valorCOFINS"] * 100 ) / oObjeto["baseCOFINS"]
      SFT->FT_BASECOF := oObjeto["baseCOFINS"]
      SFT->FT_VALCOF  := oObjeto["valorCOFINS"]
      SFT->FT_BRETCOF := oObjeto["baseCOFINS"]
      SFT->FT_VRETCOF := oObjeto["valorCOFINS"]

      SFT->FT_ALIQCSL := ( oObjeto["valorCSLL"] * 100 ) / oObjeto["baseCSLL"]
      SFT->FT_BASECSL := oObjeto["baseCSLL"]
      SFT->FT_VALCSL  := oObjeto["valorCSLL"]
      SFT->FT_BRETCSL := oObjeto["baseCSLL"]
      SFT->FT_VRETCSL := oObjeto["valorCSLL"]

    SFT->( msUnlock() )
  endif

  if oObjeto:hasProperty("valorISS")
    if SE1->(dbSeek(xFilial("SE1") + PADR(alltrim(self:cSerieNfe),TAMSX3("F2_SERIE")[1]," ") + PADR(alltrim(self:cDocumentoNfe),TAMSX3("F2_DOC")[1]," ") + space( TAMSX3("E1_PARCELA")[1] ) + "IS-"))
      recLock("SE1", .F.)
        SE1->E1_VALOR := oObjeto["valorISS"]
        SE1->E1_SALDO := oObjeto["valorISS"]
        SE1->E1_VLCRUZ := oObjeto["valorISS"]
      SE1->( msUnlock() )
    endif
  endif

  if oObjeto:hasProperty("valorCOFINS")
    if SE1->(dbSeek(xFilial("SE1") + PADR(alltrim(self:cSerieNfe),TAMSX3("F2_SERIE")[1]," ") + PADR(alltrim(self:cDocumentoNfe),TAMSX3("F2_DOC")[1]," ") + space( TAMSX3("E1_PARCELA")[1] ) + "CF-"))
      recLock("SE1", .F.)
        SE1->E1_VALOR := oObjeto["valorCOFINS"]
        SE1->E1_SALDO := oObjeto["valorCOFINS"]
        SE1->E1_VLCRUZ := oObjeto["valorCOFINS"]
      SE1->( msUnlock() )
    endif
  endif

  if oObjeto:hasProperty("valorCSLL")
    if SE1->(dbSeek(xFilial("SE1") + PADR(alltrim(self:cSerieNfe),TAMSX3("F2_SERIE")[1]," ") + PADR(alltrim(self:cDocumentoNfe),TAMSX3("F2_DOC")[1]," ") + space( TAMSX3("E1_PARCELA")[1] ) + "CS-"))
      recLock("SE1", .F.)
        SE1->E1_VALOR := oObjeto["valorCSLL"]
        SE1->E1_SALDO := oObjeto["valorCSLL"]
        SE1->E1_VLCRUZ := oObjeto["valorCSLL"]
      SE1->( msUnlock() )
    endif
  endif

  if oObjeto:hasProperty("valorINSS")
    if SE1->(dbSeek(xFilial("SE1") + PADR(alltrim(self:cSerieNfe),TAMSX3("F2_SERIE")[1]," ") + PADR(alltrim(self:cDocumentoNfe),TAMSX3("F2_DOC")[1]," ") + space( TAMSX3("E1_PARCELA")[1] ) + "IN-"))
      recLock("SE1", .F.)
        SE1->E1_VALOR := oObjeto["valorINSS"]
        SE1->E1_SALDO := oObjeto["valorINSS"]
        SE1->E1_VLCRUZ := oObjeto["valorINSS"]
      SE1->( msUnlock() )
    endif
  endif

  if oObjeto:hasProperty("valorIRRF")
    if SE1->(dbSeek(xFilial("SE1") + PADR(alltrim(self:cSerieNfe),TAMSX3("F2_SERIE")[1]," ") + PADR(alltrim(self:cDocumentoNfe),TAMSX3("F2_DOC")[1]," ") + space( TAMSX3("E1_PARCELA")[1] ) + "IR-"))
      recLock("SE1", .F.)
        SE1->E1_VALOR := oObjeto["valorIRRF"]
        SE1->E1_SALDO := oObjeto["valorIRRF"]
        SE1->E1_VLCRUZ := oObjeto["valorIRRF"]
      SE1->( msUnlock() )
    endif
  endif

  if oObjeto:hasProperty("valorPIS")
    if SE1->(dbSeek(xFilial("SE1") + PADR(alltrim(self:cSerieNfe),TAMSX3("F2_SERIE")[1]," ") + PADR(alltrim(self:cDocumentoNfe),TAMSX3("F2_DOC")[1]," ") + space( TAMSX3("E1_PARCELA")[1] ) + "PI-"))
      recLock("SE1", .F.)
        SE1->E1_VALOR := oObjeto["valorPIS"]
        SE1->E1_SALDO := oObjeto["valorPIS"]
        SE1->E1_VLCRUZ := oObjeto["valorPIS"]
      SE1->( msUnlock() )
    endif
  endif

Return 

/*/{Protheus.doc} getNatureza
  (long_description)
  @author user
  @since 24/12/2025
  @version version
  @param param_name, param_type, param_descr
  @return return_var, return_type, return_description
  /*/
Method getNatureza(oPedido) class IntegraNF
  local _cNatureza := ''
  local lISS := .F.
  local lIRRF := .F.
  local lPCC := .F.
  local lINSS := .F.
  
  if oPedido:hasProperty('valorIssRetido')
    if oPedido["valorIssRetido"] > 0
      lISS := .T.
    endif
  endif
  
  if oPedido:hasProperty('valorIRRF')
    if oPedido["valorIRRF"] > 0
      lIRRF := .T.
    endif
  endif

  if oPedido:hasProperty('valorPIS')
    if oPedido["valorPIS"] > 0
      lPCC := .T.
    endif
  endif

  if oPedido:hasProperty('valorINSS')
    if oPedido["valorINSS"] > 0
      lPCC := .T.
    endif
  endif

  //Fiz os IF's dessa maneira apenas para facilitar a visualização das naturezas
  //Podia ter feito de uma maneira mais enxuta
  if lISS .and. lIRRF .and. lPCC .and. lINSS
    _cNatureza := '010102' // Serviços com retenção de ISS, IRRF e PIS/COFINS/CSLL
  endif

  if lISS .and. lIRRF .and. lPCC .and. empty(_cNatureza)
    _cNatureza := '010105' // Serviços com retenção de ISS, IRRF e PIS/COFINS/CSLL
  endif

  if lISS .and. lIRRF .and. empty(_cNatureza)
    _cNatureza := '010103' // Serviços com retenção de ISS, IRRF e PIS/COFINS/CSLL
  endif

  if lISS .and. lPCC .and. empty(_cNatureza)
    _cNatureza := '010106' // Serviços com retenção de ISS e IRRF
  endif

  if lISS .and. lINSS .and. empty(_cNatureza)
    _cNatureza := '010108' // Serviços com retenção de ISS e IRRF
  endif

  if lISS .and. lIRRF .and. empty(_cNatureza)
    _cNatureza := '010108' // Serviços com retenção de ISS e IRRF
  endif

  if lISS .and. empty(_cNatureza)
    _cNatureza := '010104' // Serviços com retenção de ISS
  endif

  if empty(_cNatureza)
    _cNatureza := '010109' // Serviços sem retenção
  endif
  
Return _cNatureza

/*/{Protheus.doc} setEmpresa
  (long_description)
  @author user
  @since 24/12/2025
  @version version
  @param param_name, param_type, param_descr
  @return return_var, return_type, return_description
  /*/
Method setEmpresa(cCNPJ) class IntegraNF
    
    Local lRet := .F.
    
    OpenSM0(cEmpAnt)
    
    SM0->(DbGoTop())

    While SM0->(!Eof())
        If SM0->M0_CGC == cCNPJ
            cFilAnt := SM0->M0_CODFIL
            lRet    := .T.
            Exit
        EndIf
        SM0->(DbSkip())
    EndDo

Return lRet

/*/{Protheus.doc} methodName
  (long_description)
  @author user
  @since 30/12/2025
  @version version
  @param param_name, param_type, param_descr
  @return return_var, return_type, return_description
  /*/
Method getClasPro(cCodProd) class IntegraNF

    default cCodProd := ""

    if Empty(cCodProd)
        return cCodProd
    endif

    BeginSql alias 'QRYSX5'
        SELECT count(*) quantidade
          FROM %table:SX5% SX5
         WHERE SX5.X5_FILIAL = %xFilial:SX5%
           AND X5_TABELA = 'CP'
           AND X5_DESCRI = %exp:alltrim(cCodProd)%
           AND SX5.D_E_L_E_T_ <> '*'
    EndSql

    if QRYSX5->quantidade > 0

        cCodigo := getSx8Num("SX5", "X5_CHAVE")

        recLock('SX5', .T.)
            SX5->X5_FILIAL := xFilial("SX5")
            SX5->X5_TABELA := 'CP'
            SX5->X5_CHAVE := cCodigo
            SX5->X5_DESCRI := cCodProd
            SX5->X5_DESCSPA := cCodProd
            SX5->X5_DESCENG := cCodProd
        SX5->(msUnlock())
        ConfirmSx8()
    endif 

    QRYSX5->(dbclosearea())
    
Return cCodProd
